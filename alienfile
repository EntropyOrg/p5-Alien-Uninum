use alienfile;

use Config;
use Env qw(@ACLOCAL_PATH);

share {
	requires 'Alien::gmake' => 0.14;
	requires 'Path::Tiny' => 0;

	plugin Download => (
		url => 'http://billposer.org/Software/libuninum.html',
		version => qr/libuninum-([\d\.]+)\.tar\.gz/,
	);

	plugin Extract => 'tar.gz';

	plugin 'Build::Autoconf';

	patch sub {
		{
			my $unicode_h = Path::Tiny::path('unicode.h');
			my $replaced = 0;
			$unicode_h->edit_lines_utf8(sub {
				$replaced ||= s|^typedef.*UTF32;|
/* PATCH: use Perl's U32 for portability */
typedef $Config{u32type}\tUTF32;
				|;
			});
			die "Could not patch $unicode_h" unless $replaced;
		}

		{
			my $uninum_c = Path::Tiny::path('uninum.c');
			my $replaced = 0;
			$uninum_c->edit_lines_utf8(sub {
				$replaced ||= s|wcsrev|uninum_wcsrev|g;
			});
			die "Could not patch $uninum_c" unless $replaced;
		}
	};

	if( exists $ENV{MSYSTEM} ) {
		# Need for call to `autoreconf`
		# Taken from <https://github.com/jtanx/fontforgebuilds/blob/master/ffbuild.sh>.
		push @ACLOCAL_PATH,
			(
				'm4',
				"/@{[ lc $ENV{MSYSTEM} ]}/share/aclocal"
			);
	}

	build [
		sub {
			return unless exists $ENV{MSYSTEM};

			$_->remove for (
				Path::Tiny::path('config.guess'),
				Path::Tiny::path('config.sub'),
				Path::Tiny::path('configure'),
			);

			# clear PERL5LIB so that system Perl runs properly
			local $ENV{PERL5LIB} = "";

			system('sh -c "cp -v /usr/share/automake-1.16/config.guess config.guess"');
			system('sh -c "cp -v /usr/share/automake-1.16/config.sub config.sub"');

			my $exit = system('sh -c "libtoolize && autoreconf"');
			die "Non-zero exit" if $exit != 0;
		},
		"%{configure}",
		'%{make} CFLAGS=-w',
		'%{make} install',
	];
};

